plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge" version "ddb1eb01af"
}

version "2.0.0"
group "dev.gaminggeek"
sourceCompatibility = targetCompatibility = 8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientRunArgs += "--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker"
}

repositories {
    mavenCentral()

    maven {
        name "Jitpack"
        url "https://jitpack.io"
    }
    maven {
        name "Sk1er Repository"
        url "https://repo.sk1er.club/repository/maven-public"
    }
    maven {
        name = "Sponge Repository"
        url = "https://repo.spongepowered.org/repository/maven-public/"
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    include "gg.essential:loader-launchwrapper:1.1.1"
    include "com.squareup.okhttp3:okhttp:4.5.0"

    implementation("gg.essential:essential-1.8.9-forge:1492") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "asm"
    }
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.include]
    archiveClassifier.set("")

    manifest.attributes(
        "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
        "TweakOrder": "0"
    )
}

reobfJar.dependsOn tasks.shadowJar

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    filesMatching('mcmod.info') {
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
}